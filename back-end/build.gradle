plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    // id "org.web3j" version "4.8.7"
}

ext {
    generatedSourcesDir = file("${buildDir}/generated/querydsl/java")
}

group = 'com.hitpot'
version = '0.0.1.18'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir generatedSourcesDir
        }
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'https://maven.aliyun.com/nexus/content/repositories/jcenter' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'redis.clients:jedis'

    implementation 'joda-time:joda-time:2.10.8'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.24'
    implementation 'com.alibaba:easyexcel:3.2.1'
    runtimeOnly 'mysql:mysql-connector-java:8.0.32'
    implementation "org.jsoup:jsoup:1.15.4"
    implementation('org.web3j:core:4.9.6')
    implementation 'org.web3j.eth2:beacon-node-api:1.0.0'

    implementation 'cn.hutool:hutool-core:5.8.12'
    implementation 'cn.hutool:hutool-crypto:5.8.12'

    annotationProcessor("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("com.querydsl:querydsl-jpa:4.4.0")
    annotationProcessor("com.querydsl:querydsl-apt:4.4.0")

    implementation 'cn.dev33:sa-token-spring-boot-starter:1.34.0'
    implementation 'cn.dev33:sa-token-dao-redis-jackson:1.34.0'
    implementation 'cn.dev33:sa-token-jwt:1.34.0'

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

task generateQueryDSL(type: JavaCompile, group: "build", description: "Generates the QueryDSL query types") {
    source = sourceSets.main.java
    classpath = configurations.compileClasspath
    options.annotationProcessorPath = sourceSets.main.annotationProcessorPath
    options.compilerArgs = [
        "-proc:only",
        "-processor",
        "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    options.warnings = true
    destinationDir = generatedSourcesDir
}
compileJava.dependsOn 'generateQueryDSL'

task printProjectVersion {
    doLast {
        println project.name
        println project.version
        sourceSets.all { set ->
            println("${set.name} -> ${set.java.srcDirs}")
        }
    }
}

task printVersion(type: DefaultTask, group: "other") {
    doLast {
        println project.version
    }
}
